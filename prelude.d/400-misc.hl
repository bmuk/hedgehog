;; This file is part of Hedgehog LISP standard library.
;; Copyright (C) 2003, 2004 Oliotalo Ltd.
;; See file LICENSE.BSD for pertinent licensing conditions.
;;
;; Author: Lars Wirzenius <liw@iki.fi>
;;         Kenneth Oksanen <cessu@iki.fi>


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: Miscellaenous
;;
;; This section contains functions that have no better section.


;; Check that the version of the interpreter we're running under is 
;; compatible with the application. If not, panic.

(def (require-version required)
    (def (parse-version string)
	(map (fn (s) (atoi s 10)) (strsplit string ".")))
    (def (acceptable-version? version-parts required-parts)
    	(and (= (abs (nth version-parts 0)) (nth required-parts 0))
	     (>= (abs (nth version-parts 1)) (nth required-parts 1))
	     (>= (abs (nth version-parts 2)) (nth required-parts 2))))
    (set version-parts (parse-version (hedgehog-version)))
    (set required-parts (parse-version required))
    (if (not (acceptable-version? version-parts required-parts))
    	(panic "\nAt least version " 
	    	required
	       " is required and the interpreter is version " 
	       (hedgehog-version) ".\n\n")))

;; Print out the values of the arguments and then a final newline.
;; This differs from the built-in print function only in that the
;; final newline is implicit.

(def (pr ... args)
    (def (pr-helper args)
	(if (nil? args)
	    (print "\n")
	    (do (print (car args))
		(tailcall (pr-helper (cdr args))))))
    (pr-helper args))


;; If 'trace?' is true, write a log message, prefixed with 'prefix' and
;; consisting of the values in 'values'. No space is printed between 
;; values.

(def (trace trace? prefix values)
    (if trace?
    	(do (print prefix)
	    (for-each print values)
	    (print "\n"))))
